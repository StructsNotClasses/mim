:on_no_playback
:load_script scripts/shuffle.tengo

:bind p
:load_script scripts/pause.tengo

:bind q
:load_script scripts/skip.tengo

:bind j
:begin
selectDown()
:end move_down

:bind k
:begin
selectUp()
:end move_up

:bind h
:begin
selectEnclosing()
:end select_enclosing

:bind l
:begin
if selectedIsDir() {
    toggle(currentIndex())
} else {
    playSelected()
}
:end play_or_toggle

:bind a
:load_script scripts/play_album.tengo

:bind C
:load_script scripts/close_all.tengo

:bind n
:begin
matchIndex := nextMatch(currentIndex() + 1)
if matchIndex != -1 {
    selectIndex(matchIndex)
} else if matchIndex = nextMatch(0); matchIndex != -1 {
    selectIndex(matchIndex)
}
:end select_next_match_wrapped

:bind N
:begin
matchIndex := prevMatch(currentIndex() - 1)
if matchIndex != -1 {
    selectIndex(matchIndex)
}
:end select_previous_match

:bind /
:begin
infoPrint("Input search: ")
line := ""
ch := getChar()
for ; ch != '\n'; ch = getChar() {
    line = line + string(ch)
    infoPrint(string(ch))
}
infoPrintln()
setSearch(line)
matchIndex := nextMatch(currentIndex() + 1)
if matchIndex != -1 {
    selectIndex(matchIndex)
}
:end input_search

:bind ?
:begin
selectWrappedNextMatch := func() {
    matchIndex := nextMatch(currentIndex())
    if matchIndex != -1 {
        selectIndex(matchIndex)
    } else if matchIndex = nextMatch(0); matchIndex != -1 {
        selectIndex(matchIndex)
    }
}
infoPrint("Input search: ")
line := ""
ch := getChar()
for ; ch != '\n'; ch = getChar() {
    line = line + string(ch)
    infoPrint(string(ch))
    setSearch(line)
    selectWrappedNextMatch()
}
infoPrintln()
setSearch(line)
selectWrappedNextMatch()
:end live_search

:bind .
:begin
send("seek +15")
:end seek_forward

:bind ,
:begin
send("seek -15")
:end seek_back

:bind g
:begin
selectIndex(0)
:end jump_to_top

:bind G
:begin
selectIndex(itemCount() - 1)
:end jump_to_bottom

:bind =
:begin
send("volume +10 0")
:end volume_up

:bind -
:begin
send("volume -10 0")
:end volume_down

:new_command s scripts/enable_shuffle.mim
:new_command ns scripts/enable_sequential.mim
:new_command as scripts/enable_album_shuffle.mim

:alias q :exit
